#!/usr/bin/env perl
use warnings;
use strict;

use Getopt::Long qw(:config no_ignore_case);;
use Pod::Usage;

use Fasta::Parser;
use Fasta::Seq;

use Fastq::Parser;
use Fastq::Seq;

=head1 SYNOPSIS

  seq-frag MODE -l LENGTH -c COVERAGE [options ..] < FASTA
  # 50X 100bp single end reads
  seq-frag se -l 100 -c 50 < genome.fa
  # 50X 100bp paired end, 180bp insert
  seq-frag pe -l 100 -c 50 -i 180 < genome.fa
  # 50X mate pair, 2000bp insert
  seq-frag mp -l 100 -c 50 -i 2000 < genome.fa
  # 20X pacbio style fragments, mean length 2000bp
  seq-frag pacbio -l 2000 -c 20 < genome.fa

=head1 OPTIONS

=over

=item -m/--mode

  se       single end, fixed read length, fastq
  pe       paired end, fixed read length, insert size from normal
           dist., fastq, interleaved opposing-inward (><)
  mp       as pe, but opposing outward (<>)
  pacbio   length from neg. binomial dist., mean at -l, fastq
  contig   non-overlapping, length from neg. binomial dist., mean at -l, fasta

=item -l/--length

Length of reads/sub-sequences to generate.

=item -c/--coverage

Coverage of sampled set.

=item [-i/--insert-size] [180]

Insert size for pe/mp mode.

=item [-s/--systematic] [OFF]

Use systematic sampling with fixed insert-size and fixed distance between
fragments rather than sampling from distributions (only for pe, ignored for
other modes)

=item [-N/--N-max] [OFF]

Maximum number of N's per read.

=item [-Q/--quality] [20:0]

Phred score of minimum quality : maximum number of bases below minimum quality.

=item [-P/--phred64] [33]

Toggle phred offset from 33 to 64.

=item [-r/--prefix]

Read name prefix.

=item [-p/--progress]

Show progress in processed Mbp.

=back

=cut

=head1 MAIN

SE
  fixed length, fastq
PE/MP
  fixed length, varying insert
PB
  varying length
contigs
  varing length, non-overlapping

=cut

my $VERSION = 0.2.0;

my %def = (
           mode => undef,
           length => undef,
           insert_size => 180,
           coverage => undef,
           prefix => "r",
           region_length => 800_000,
           region_length_factor => 100,
           nmax => undef,
           quality => "20:0",
           phred_offset => 33,

       );

my %opt = ();
GetOptions(                     # use %opt (Cfg) as defaults
           \%opt, qw(
                        mode|m=s
                        length|l=i
                        coverage|c=i
                        insert_size|insert-size|i=i
                        systematic|s!
                        nmax|N:i
                        quality|Q=s
                        phred64|P!
                        prefix|r=s
                        progress|p!
                        version|V!
                        debug|D!
                        help|h!
                   )
          ) or die 'Failed to "GetOptions"';

# help
$opt{help} && pod2usage(1);

# version
if ($opt{version}) {
    print "$VERSION\n";
    exit 0;
}

# primary options length
if(!$opt{mode} && @ARGV && !($ARGV[0] =~ /^-/)){
        $opt{mode} = shift @ARGV;
}

%opt = (%def, %opt);

$opt{mode} || pod2usage("MODE required. Supported: se, pe, mp, pacbio and contig.\nSee --help for details");
(my $mode = grep{$opt{mode} eq $_}qw(pe se mp pacbio contig)) || pod2usage("unknown MODE $opt{mode}");
$opt{length} || pod2usage("required: --length");
if ( $opt{mode} ne "contig" ){
    $opt{coverage} || pod2usage("required: --coverage");
}else {
    $opt{coverage} //= 1; # default cov of 1 for contig mode
}
my $insert_lib_mode = grep{$opt{mode} eq $_}qw(pe mp);
if ($insert_lib_mode && $opt{insert_size} < $opt{length}) {
    die "Insert size smaller than read length does not make sense\n";
}

my %frag = (
    se => \&frag_se,
    pe => \&frag_pe,
    mp => \&frag_pe,
    pacbio => \&frag_pacbio,
    contig => \&frag_contig,
);

if (($opt{mode} eq "pe" || $opt{mode} eq "mp") && !$opt{systematic}) {
    print STDERR "non-systematic paired-end sampling, loading Math::Random\n";
    require Math::Random;
}

# precompile low quality transiteration
$opt{phred_offet} = 64 if $opt{phred64};
my ($Q, $Qmax, $Qtr);

if (defined($opt{quality})) {
    ($Q, $Qmax) = split(":", $opt{quality}, 2);
    $Qmax//=0;
    die "-Q needs to be of format INT:INT\n" unless ($Q =~ /^\d+$/ && $Qmax =~ /^\d+$/);
    my $Qlow = join("", map{chr($_+ $opt{phred_offset})}(0..$Q-1));
    $Qtr = eval 'sub { $_[0]'." =~ tr/\Q$Qlow\E// }" or die $@;
}

# fragment counter
my $c = 1;

my $fp = Fasta::Parser->new(file=>$ARGV[0])->check_format ||
    Fastq::Parser->new(file=>$ARGV[0])->check_format;

die "Input file neither Fasta nor Fastq\n" unless $fp;

my $FQ = ref($fp) eq 'Fastq::Parser';

# make sure regions is always a lot bigger than insert size
if ($opt{region_length} <= $opt{insert_size} * $opt{region_length_factor}){
    $opt{region_length} = $opt{insert_size} * $opt{region_length_factor}
}

my $bp=0;
while (my $fa = $fp->next_seq) {
    # seq length is bottle neck (not sort or rand)
    # => split long seqs into manageable regions
    $bp+=length($fa->seq);
    $frag{$opt{mode}}($_) for fa_regions($fa);
    if ( $opt{progress} ) {
        printf STDERR ("\r%0.1f Mbp", $bp / 1_000_000);
    }
}
print STDERR "\n" if $opt{progress};






=head2 fa_regions

Split long fastas in regions to speed up substring

=cut

sub fa_regions{
    my $fa = shift;
    my $seq_len = length($fa->seq);
    return ($fa) if ( $seq_len <= $opt{region_length});

    my @fa_regions;
    my @coords;

    my $l = $opt{region_length} + ($insert_lib_mode ? $opt{insert_size} : $opt{length});
    my $o = 0;
    while ($o+$l < $seq_len) {
        push @coords, [$o, $l];
        $o+=$opt{region_length};
    }
    push @coords, [$o];
    @fa_regions = $fa->substr_seq(@coords);

    return @fa_regions;
}


=head2 frag_se

=cut

sub frag_se{
    my $fa = shift;
    my $seq_len = length($fa->seq);
    return if $seq_len < $opt{length};
    my $read_len = $opt{length};
    my $frag_len = $opt{length};
    my $frag_num = $seq_len * ( $opt{coverage} / $read_len );

    # sort is bottle neck, either ignore and return reads in random order
    # or limit manageable regions for long seq
    my @frag_offs;
    if ($opt{systematic}) {
        @frag_offs[0,1] = (0, int(($seq_len - $read_len) / $frag_num)); # frag step
        for (my $i=2; $i< $frag_num; $i++) {
            $frag_offs[$i] = $frag_offs[$i-1] + $frag_offs[1];
        }
    }else {
        @frag_offs = sort{$a<=>$b}(Math::Random::random_uniform_integer($frag_num, 0, $seq_len-$frag_len));
    }

    foreach my $frag_off ( @frag_offs) {

        my $frag = Fastq::Seq->new(
            '@'.$opt{prefix}.$c,
            substr($fa->seq, $frag_off, $read_len),
            "+",
            ref($fp) eq 'Fastq::Parser' ? substr($fa->qual, $frag_off, $read_len) : "I" x $read_len
        );

        # both strands
        int(rand(2)) && $frag->reverse_complement;

        print $frag;

        $c++;
    }
}

=head2 frag_pacbio

Math::Random::random_negative_binomial($n,$ne,$p)

mean = $ne/$p
$p == 1
  largest spread, med << mean, mass at 0
$p++
  mass shift from 0 mean

=cut

sub frag_pacbio{
    my $fa = shift;
    my $seq_len = length($fa->seq);
    return if $seq_len < $opt{length};
    my $read_len = $opt{length};
    my $frag_num = $seq_len * ( $opt{coverage} / $read_len );

    my @frag_offs;
    my @frag_lens;

    if ($opt{systematic}) {
        @frag_lens = map{$read_len}1..$frag_num;
        @frag_offs[0,1] = (0, int(($seq_len - $read_len) / $frag_num)); # frag step
        for (my $i=2; $i< $frag_num-1; $i++) {
            $frag_offs[$i] = $frag_offs[$i-1] + $frag_offs[1];
        }
    }else {
        @frag_offs = sort{$a<=>$b}(Math::Random::random_uniform_integer($frag_num, 0, $seq_len-$read_len));
        @frag_lens = Math::Random::random_negative_binomial($frag_num,5,5/$read_len);
    }

    for ( my $i=0; $i<@frag_offs; $i++) {
        my $frag_off = $frag_offs[$i];
        my $frag_len = int($frag_lens[$i]);
        next if $frag_off + $frag_len > $seq_len;
        #print STDERR join("\t", $frag_off, $frag_len, $frag_off+$frag_len, $seq_len),"\n";
        my $frag = Fastq::Seq->new(
            '@'.$opt{prefix}.$c,
            substr($fa->seq, $frag_off, $frag_len),
            "+",
            ref($fp) eq 'Fastq::Parser' ? substr($fa->qual, $frag_off, $read_len) : "I" x $read_len
        );

        # both strands
        int(rand(2)) && $frag->reverse_complement;

        print $frag;

        $c++;
    }
}


=head2 frag_pe

=cut

sub frag_pe{
    my $fa = shift;
    my $seq = $fa->seq;
    my $qual = $fa->qual if $FQ;
    my $seq_len = length($seq);

    return if $seq_len < $opt{insert_size};
    my $read_len = $opt{length};
    my $frag_num = int($seq_len * ( $opt{coverage} / $read_len/2 )); # pe
    my $frag_qual =  "I" x $read_len;
    my $frag_shift = ($seq_len - $opt{insert_size}) / $frag_num;
    my $Nmax = $opt{nmax};

    if ($opt{systematic}) {

        my $frag_off = 0;
        my $frag_len = $opt{insert_size};
        my $frag_pre = $opt{prefix};
        my $PE = $opt{mode} eq 'pe';

        for (my $i=0; $i<=$frag_num; $i++ ) {
            my $frag_off_int = int($frag_off);
            $frag_off+=$frag_shift; # increment regardless of ignoring reads

            my $fs1 = uc(substr($seq, $frag_off_int, $read_len));
            defined($Nmax) && $fs1 =~ tr/N// > $Nmax && next;
            my $fq1 = $FQ ? substr($qual, $frag_off_int, $read_len) : $frag_qual;
            defined($Q) && $Qtr->($fq1) > $Qmax && next;

            my $fs2 = uc(substr($seq, $frag_off_int + $frag_len - $read_len, $read_len));
            defined($Nmax) && $fs2 =~ tr/N// > $Nmax && next;
            my $fq2 = $FQ ? substr($qual, $frag_off_int + $frag_len -$read_len, $read_len) : $frag_qual;
            defined($Q) && $Qtr->($fq2) > $Qmax && next;

            if ($PE){ ## pair-end or mate-pair
                $fs2 = seqrc($fs2);
                $fq2 = reverse($fq2);
            }else {
                $fs1 = seqrc($fs1);
                $fq1 = reverse($fq1);
            }

            # both strands
            int(rand(2)) && (($fs1,$fs2)=($fs2,$fs1));
            print '@'.$frag_pre.$c."/1\n",$fs1,"\n+\n",$fq1,"\n";
            print '@'.$frag_pre.$c."/2\n",$fs2,"\n+\n",$fq2,"\n";
            $c++;
        }

    }else {
        my @frag_offs = sort{$a<=>$b}(Math::Random::random_uniform_integer($frag_num, 0, $seq_len-$opt{insert_size}));
        my @frag_lens = Math::Random::random_normal($frag_num, $opt{insert_size}, $opt{insert_size}*0.12);

        for ( my $i=0; $i<@frag_lens; $i++) {
            my $frag_off = $frag_offs[$i];
            my $frag_len = int($frag_lens[$i]);
            next if $frag_off + $frag_len > $seq_len;
            $frag_len < $read_len && ($frag_len=$read_len);
            my $frag = Fasta::Seq->new(id=>"dummy", seq => substr($fa->seq, $frag_off, $frag_len));

            # both strands
            my $rand_rev = int(rand(2));
            $rand_rev && $frag->reverse_complement;

            # for fasta input, qual is static: "I" x $read_len
            if (ref($fp) eq 'Fastq::Parser') {
                $frag_qual = substr($fa->qual, $frag_off, $frag_len);
                if ($rand_rev) {
                    $frag_qual = reverse($frag_qual);
                }
            }

            my $r = Fastq::Seq->new(
                '@'.$opt{prefix}.$c."/1",
                substr($frag->seq, 0, $read_len),
                "+",
                substr($frag_qual, 0, $read_len)
            );
            my $m = Fastq::Seq->new(
                '@'.$opt{prefix}.$c."/2",
                substr($frag->seq, -$read_len),
                "+",
                substr($frag_qual, -$read_len)
            );

            $opt{mode} eq 'pe' ? $m->reverse_complement : $r->reverse_complement;
            print $r, $m;
            $c++;
        }
    }
}

=head2 frag_contig

=cut

sub frag_contig{
    my $fa = shift;
    my $seq_len = length($fa->seq);
    return if $seq_len < $opt{length};
    my $read_len = $opt{length};
    my $frag_num = $seq_len / $read_len;
    # seq length is bottle neck (not sort or rand)
    # => split long seqs (>100k) into manageable regions
    my $fuzz = Math::Random::random_negative_binomial(1,5,0.01)-400;
    $fuzz = 0 if $fuzz < 0 || $fuzz > $seq_len; # respect max seq len

    my @frag_offs = sort{$a<=>$b}(Math::Random::random_uniform_integer($frag_num, 0, $seq_len-$read_len), $fuzz);
    my @frag_lens;

    for (my $i=0; $i<$#frag_offs; $i++) {
        my $len = $frag_offs[$i+1] - $frag_offs[$i];
        my $fuzz = Math::Random::random_negative_binomial(1,5,0.01) - 400; # max 400 bp overlap
        $fuzz = int($len/2) if $len - $fuzz < 1; # no neg fuzz
        $fuzz = 0 if $frag_offs[$i] + $fuzz > $seq_len; # respect max seq len
        $frag_lens[$i] = $len - $fuzz;
    }

    $fuzz = Math::Random::random_negative_binomial(1,5,0.01)-400;
    $fuzz = 0 if $fuzz < 0 || $fuzz > $seq_len; # respect max seq len
    push @frag_lens, $seq_len - $frag_offs[$#frag_offs] - $fuzz;
    if ($frag_lens[-2] - $frag_lens[-1] > -10) {
        pop @frag_offs;
        pop @frag_lens;
    }


    for ( my $i=0; $i<@frag_offs; $i++) {
        my $frag_off = $frag_offs[$i];
        my $frag_len = int($frag_lens[$i]);
        if ($frag_off + $frag_len > $seq_len){next;};
        #print STDERR join("\t", $frag_off, $frag_len, $frag_off+$frag_len, $seq_len),"\n";
        my $frag = Fasta::Seq->new(
            id => '>'.$opt{prefix}.$c,
            seq => substr($fa->seq, $frag_off, $frag_len),
        );

        # both strands
        int(rand(2)) && $frag->reverse_complement;

        print $frag;

        $c++;
    }
}


sub seqrc{
    $_[0] =~ tr/ATGCatgc/TACGtacg/;
    return scalar reverse $_[0];
}
